PREFIX oj: <http://ontojob.at/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX oj: <http://ontojob.at/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX oj: <http://ontojob.at/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX oj: <http://ontojob.at/>
PREFIX oj: <http://ontojob.at/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX oj: <http://ontojob.at/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX oj: <http://ontojob.at/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX lang: <http://ontologi.es/lang/core#>
PREFIX lang: <http://ontologi.es/lang/core#>
PREFIX cdm: <http://publications.europa.eu/ontology/cdm#>
# Load data for the GDPR regulation: 'Regulation (EU) 2016/679' 

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX cdm:<http://publications.europa.eu/ontology/cdm#>
PREFIX skos:<http://www.w3.org/2004/02/skos/core#>
PREFIX dc:<http://purl.org/dc/elements/1.1/>
PREFIX lang:<http://publications.europa.eu/resource/authority/language/>
PREFIX xsd:<http://www.w3.org/2001/XMLSchema#>
PREFIX rdf:<http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl:<http://www.w3.org/2002/07/owl#>

SELECT DISTINCT 
    ?date ?workId as ?id 
    ?title 
    ?baseTitle
    ?eli 
    ?ecli
    ?force 
    group_concat(DISTINCT ?dateForce;SEPARATOR=",") as ?dateForce 
    ?oj 
    ?ojId 
    ?ojResourceUrl
    ?consolidatedDate 
    ?consolidatedEli 
    ?consolidatedId
    ?dateValidity
    ?repealCelexId 
    ?repealEli
    ?lastRepealCelexId
    ?lastRepealEli
WHERE {  
    ?exp cdm:expression_belongs_to_work ?s .
    ?exp cdm:expression_title ?title_ .
    ?exp cdm:expression_uses_language lang:ENG .

    ?s cdm:work_date_document ?date .
    ?s rdf:type ?type .
    ?s cdm:work_id_document ?workId.
    OPTIONAL {
        ?s cdm:case-law_ecli ?ecli
    }
    OPTIONAL {
        ?s cdm:resource_legal_date_end-of-validity ?dateValidity .
    }
    FILTER (?workId IN ("celex:32016R0679", "celex:32016R0679"^^xsd:string))
    OPTIONAL {
        # CONSOLIDATING ACT
        ?actConsolidated cdm:act_consolidated_based_on_resource_legal ?s .
        ?actConsolidated cdm:act_consolidated_date ?consolidatedDate .
        ?actConsolidated cdm:resource_legal_eli ?consolidatedEli . 
        ?actConsolidated cdm:work_id_document ?consolidatedId_ .
        FILTER regex(str(?consolidatedId_), "celex:")     
        # Latest consolidating act only
        FILTER not exists {
            ?actConsolidated2 cdm:act_consolidated_based_on_resource_legal ?s .
            ?actConsolidated2 cdm:act_consolidated_date ?consolidatedDate2
            FILTER (?consolidatedDate2 > ?consolidatedDate)
        }
        BIND(SUBSTR(?consolidatedId_, 7) as ?consolidatedId)
    }
    OPTIONAL {
        # REPEALING ACT
        ?actRepeal cdm:resource_legal_repeals_resource_legal ?s .
        ?actRepeal cdm:resource_legal_eli ?repealEli . 
        ?actRepeal cdm:work_id_document ?repealCelexId_.
        FILTER regex(str(?repealCelexId_), "celex:")                    
        BIND(SUBSTR(?repealCelexId_, 7) as ?repealCelexId)

        OPTIONAL {
            # REPEALING (LAST ACT)
            ?lastActRepeal cdm:resource_legal_repeals_resource_legal+ ?s .
            ?lastActRepeal cdm:resource_legal_eli ?lastRepealEli . 
            ?lastActRepeal cdm:work_id_document ?lastRepealCelexId_.
            FILTER regex(str(?lastRepealCelexId_), "celex:")                    
            FILTER not exists {
               ?actRepeal_ cdm:resource_legal_repeals_resource_legal ?lastActRepeal .
            } 
            BIND(SUBSTR(?lastRepealCelexId_, 7) as ?lastRepealCelexId)
        }
    }
    OPTIONAL {
        ?s cdm:resource_legal_eli ?eli .
    }
    OPTIONAL {
        # FORCE
        ?s cdm:resource_legal_in-force ?force .
        ?s cdm:resource_legal_date_entry-into-force ?dateForce .
    }        
    OPTIONAL {
        # MANIFEST 
        ?manif cdm:manifestation_manifests_expression ?exp. 
        ?manif cdm:manifestation_type ?manifType .

        # MANIFEST OJ PAGE
        ?manif cdm:manifestation_official-journal_part_page_first ?ojPageFirst.
        ?manif cdm:manifestation_official-journal_part_page_last ?ojPageLast.
        ?manif cdm:manifestation_part_of_manifestation ?parentManif.
        ?parentManif owl:sameAs ?langSpecificManif.
        FILTER(STR(?manifType)="print" || BOUND(?ojPageFirst)) .

        OPTIONAL {
            ?manif owl:sameAs ?manifOjResourceUrl .
            # We need to use the MANIFEST to get to the OJ
            FILTER (STRSTARTS(STR(?manifOjResourceUrl), "http://publications.europa.eu/resource/oj/"))
        }

        # OJ info
        ?s cdm:resource_legal_published_in_official-journal ?q .
        ?q cdm:official-journal_part_of_collection_document ?ojPart .
        ?q cdm:official-journal_number ?ojNumber .
        OPTIONAL {
            ?q cdm:official-journal_class ?ojClass .
        }
        ?ojPart skos:prefLabel ?ojPartLabel .
        ?q cdm:official-journal_volume ?ojVolume .
        ?q cdm:publication_general_date_publication ?ojDatePublication .

        # cross match with parent OJ resource
        ?q owl:sameAs ?mainOjResourceUrl .
        ?q cdm:work_id_document ?ojId.

        # multiple OJ publications can cause trouble so we cross-match the OJ url with the manifest's (where available)
        FILTER (!BOUND(?langSpecificManif) || STRSTARTS(STR(?langSpecificManif), STR(?mainOjResourceUrl)))
        FILTER (STRSTARTS(STR(?ojId), "oj:") AND STRSTARTS(STR(?mainOjResourceUrl), "http://publications.europa.eu/resource/oj/")) 
        BIND(?mainOjResourceUrl as ?ojResourceUrl)

        # Build OJ information
        BIND(IF(BOUND(?ojPartLabel), 
        CONCAT(CONCAT(?ojPartLabel, " ", 
        CONCAT(?ojNumber, 
        CONCAT(IF (STR(?ojClass) != "R", ?ojClass, ""),
        CONCAT(", ", CONCAT($ojDatePublication,  
        CONCAT(", Vol. ", 
        CONCAT(?ojVolume, 
            CONCAT(" P. ", 
            CONCAT(xsd:integer(?ojPageFirst), 
            CONCAT(" - ", xsd:integer(?ojPageLast)))))))))))), "") as ?oj) .
        FILTER ( lang(?ojPartLabel) = "en" )
    }

    BIND(CONCAT(?title_, IF(BOUND(?ecli), CONCAT("\nECLI identifier: ", ?ecli), "")) as ?title) 
    BIND(?title_ as ?baseTitle)
}